#!/usr/bin/env zsh

_mc-update() {
    local -a subcommands
    subcommands=(
        'list-templates:List semua template'
        'list-projects:List semua project'
        'add-template:Tambah template baru'
        'add-project:Tambah project baru'
        'remove-project:Hapus project'
        'set-version:Ubah versi template project'
        'run:Install packages untuk semua/semua versi'
        'run-project:Install packages untuk project spesifik'
        'run-folder:Install packages untuk folder spesifik'
    )

    local -a version_opts project_names template_names
    local state curcontext="$curcontext"

    _arguments -C \
        '1: :->subcommand' \
        '*:: :->args'

    case $state in
        subcommand)
            _describe -t commands 'mc-update subcommands' subcommands
            ;;
        args)
            case ${words[1]} in
                add-template)
                    if [[ $CURRENT -eq 2 ]]; then
                        _message "Nama template"
                    elif [[ $CURRENT -eq 3 ]]; then
                        _message "Versi template"
                    elif [[ $CURRENT -eq 4 ]]; then
                        _message "Package npm (contoh: react react-dom)"
                    fi
                    ;;
                add-project)
                    if [[ $CURRENT -eq 2 ]]; then
                        _message "Nama project"
                    elif [[ $CURRENT -eq 3 ]]; then
                        _files -/
                    elif [[ $CURRENT -eq 4 ]]; then
                        # Ambil daftar template dari config
                        if [[ -f ~/.mc/.mc-template.json ]]; then
                            template_names=(${(f)"$(jq -r 'keys[]' ~/.mc/.mc-template.json 2>/dev/null)"})
                            _describe -t templates 'Available templates' template_names
                        fi
                    elif [[ $CURRENT -eq 5 ]]; then
                        # Ambil versi untuk template yang dipilih
                        local selected_template="${words[4]}"
                        if [[ -n "$selected_template" && -f ~/.mc/.mc-template.json ]]; then
                            version_opts=(${(f)"$(jq -r ".\"$selected_template\" | keys[]" ~/.mc/.mc-template.json 2>/dev/null)"})
                            _describe -t versions 'Available versions' version_opts
                        fi
                    fi
                    ;;
                remove-project|run-project)
                    if [[ $CURRENT -eq 2 ]]; then
                        # Ambil daftar project dari config
                        if [[ -f ~/.mc/.mc-projects.json ]]; then
                            project_names=(${(f)"$(jq -r 'keys[]' ~/.mc/.mc-projects.json 2>/dev/null)"})
                            _describe -t projects 'Available projects' project_names
                        fi
                    fi
                    ;;
                set-version)
                    if [[ $CURRENT -eq 2 ]]; then
                        # Ambil daftar project
                        if [[ -f ~/.mc/.mc-projects.json ]]; then
                            project_names=(${(f)"$(jq -r 'keys[]' ~/.mc/.mc-projects.json 2>/dev/null)"})
                            _describe -t projects 'Available projects' project_names
                        fi
                    elif [[ $CURRENT -eq 3 ]]; then
                        # Ambil template dari project yang dipilih, lalu daftar versinya
                        local selected_project="${words[2]}"
                        if [[ -n "$selected_project" && -f ~/.mc/.mc-projects.json ]]; then
                            local template=$(jq -r ".\"$selected_project\".template" ~/.mc/.mc-projects.json 2>/dev/null)
                            if [[ -n "$template" && -f ~/.mc/.mc-template.json ]]; then
                                version_opts=(${(f)"$(jq -r ".\"$template\" | keys[]" ~/.mc/.mc-template.json 2>/dev/null)"})
                                _describe -t versions 'Available versions' version_opts
                            fi
                        fi
                    fi
                    ;;
                run)
                    if [[ $CURRENT -eq 2 ]]; then
                        # Ambil semua versi yang unik dari semua template
                        if [[ -f ~/.mc/.mc-template.json ]]; then
                            version_opts=(${(f)"$(jq -r '.[] | keys[]' ~/.mc/.mc-template.json 2>/dev/null | sort -u)"})
                            _describe -t versions 'Available versions' version_opts
                        fi
                    fi
                    ;;
                run-folder)
                    if [[ $CURRENT -eq 2 ]]; then
                        _files -/
                    elif [[ $CURRENT -eq 3 ]]; then
                        # Ambil daftar template
                        if [[ -f ~/.mc/.mc-template.json ]]; then
                            template_names=(${(f)"$(jq -r 'keys[]' ~/.mc/.mc-template.json 2>/dev/null)"})
                            _describe -t templates 'Available templates' template_names
                        fi
                    elif [[ $CURRENT -eq 4 ]]; then
                        # Ambil versi untuk template yang dipilih
                        local selected_template="${words[3]}"
                        if [[ -n "$selected_template" && -f ~/.mc/.mc-template.json ]]; then
                            version_opts=(${(f)"$(jq -r ".\"$selected_template\" | keys[]" ~/.mc/.mc-template.json 2>/dev/null)"})
                            _describe -t versions 'Available versions' version_opts
                        fi
                    fi
                    ;;
            esac
            ;;
    esac
}

compdef _mc-update mc-update
